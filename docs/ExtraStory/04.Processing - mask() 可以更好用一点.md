# 让遮罩更好用一点 - Processing
![image](https://user-images.githubusercontent.com/20842136/138540190-59afc9ae-3e2f-4fce-a2d6-8da83c584678.png)

# mask() 遮罩
[mask()](https://processing.org/reference/mask_.html) 是 Processing 中实现遮罩效果的方法，文档中指出 mask() 需要传入 PImage 类型的参数，也就是说只能用图片做遮罩，在实际使用中可用范围极其有限，比如想用在 Processing 生成的图形、文字做遮罩就很难实现。

我也忘了是自己想到的，还是在查了一大堆资料后，大脑自动拼合了有效信息，总之有办法把 `mask()` 的限制放宽一点。

显示 `mask()` 的方法是 [image()](https://processing.org/reference/image_.html)，现在我们知道 `image()` 除了能显示图片还能显示 `mask()`（mask 本质上就是图像）。如果你用过 [PGraphics](https://processing.org/reference/PGraphics.html)，就知道它也是通过 `image()` 来显示的。

``` Java
image(img, 0, 0);
image(mask, 0, 0);
image(Graphics, 0, 0)
```

我不懂 Java（Processing 基于 Java）的底层原理，但是看上面的比较，我猜 `img / mask / Graphics` 它们在底层的数据类型是差不多的，既然 `image` 能作为 `mask()` 的传参，`Graphics` 是不是也可以？

# 果然可以

![ezgif com-gif-maker (6)](https://user-images.githubusercontent.com/20842136/138544543-7cbf88f1-72b7-4cba-a582-ac070dd4849b.gif)

## 核心思路

### 创建 Graphics
![image](https://user-images.githubusercontent.com/20842136/138544601-c5a10c4e-d160-416f-ba41-f2de30174755.png)
创建两个 Graphics（mask、source），分别用于显示层和遮罩层。

### 设置 Graphics
![image](https://user-images.githubusercontent.com/20842136/138544696-1fe6e6b5-d43a-47b9-84c3-1038968c318c.png)
分别设置两个 Graphic 中的内容，比如：
- 显示层内容为文本；
- 遮罩层用圆形作为遮罩；

### 设置 Mask
![image](https://user-images.githubusercontent.com/20842136/138544730-510ff5fb-d8f0-458c-9693-d5d6ffc5a744.png)
- 为显示层设置遮罩；
- 通过 image() 启动显示层；

**完整代码：**
``` Java
PGraphics mask;
PGraphics source;

void setup() {
  size(800, 800);
  pixelDensity(2);
  smooth();
  textAlign(CENTER, CENTER);

  mask = createGraphics(width, height);
  source = createGraphics(width, height);
}

void draw() {
  background(#F2B5D4);
  
  textSize(743);
  text("A", width/2, height/2 - 137);

  setMask();
  setSource();
  source.mask(mask);
  image(source, 0, 0);
}

void setMask() {
  mask.beginDraw();
  mask.clear();
  float size = map(mouseX, 0, width, 0, width);
  mask.circle(width/2, height/2, size);
  mask.endDraw();
}

void setSource() {
  source.beginDraw();
  source.clear();
  source.fill(-1);
  source.textAlign(CENTER, CENTER);
  source.textSize(676);
  source.text("B", width/2, height/2 - 107);
  source.endDraw();
}
```


这样就实现了用 [PGraphics](https://processing.org/reference/PGraphics.html) 做遮罩的效果。相比用图片做遮罩，Graphics 的可用性会高出很多，非常灵活，你可以任意设置遮罩的形态，无论图形还是文本，只要能显示在画面上的内容都可以，甚至可以做成动态遮罩。

灵活性高意味着有更多可能性，上面的举例只是最基础的用法，希望你能在此基础上继续探索，做出更多、更好玩的「可能」。